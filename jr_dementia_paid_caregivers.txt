= V4 Outline MultiLine NoSorting TabWidth=30

H="Outline"
/* 
********************HEADING******************** 

Project Name: Adverse Consequences of Unmet Need among Probable Dementia Patients

Date Started: 7/18/19

Primary Investigator: Jenny Reckrey
Funding Source: GEMSSTAR

Created by: EBL

Primary Analyst: EBL
Secondary Analyst: MH

Datasets Used: nhats


*/
 
//STATA
// Global Macros use $ symbol to be called. 

//Clean NHATS 
global nhats "D:\nhats\shared\base_data\nhats cleaned"

//Intermediate Data Path
global intpath "D:\nhats\Projects\caregiving\jr_dem_paid_cgs_and_unmet_needs\data\int_data"

// Final Data Path
global datapath "D:\NHATS\Projects\caregiving\jr_dem_paid_cgs_and_unmet_needs\data\final_data"

//Log files path
global logpath "D:\NHATS\Projects\caregiving\jr_dem_paid_cgs_and_unmet_needs\output\logs"

//Log files path
global outpath "D:\NHATS\Projects\caregiving\jr_dem_paid_cgs_and_unmet_needs\output\in_progress"

H="Sample derivation"
//Sample derivation

use "D:\NHATS\Shared\base_data\NHATS cleaned\sp_round_1_7.dta", clear



local samplevars all inrange spivw nonh nolml prob_dem anyh

gen all=1
gen inrange=inlist(wave,1,5)
gen spivw=sp_ivw==1
gen nonh=!nhres
gen nolml=!lml
gen anyh=n_help>0
label var all "All NHATS waves 1-7"
label var inrange "Wave 1 and/or 5"
label var spivw "SP interview completed"
label var nonh "Community dwelling at interview"
label var nolml "Non-deceased (not LML ivw)"
label var anyh "Receives any help"

local rn : word count `samplevars'

mat tab=J(`rn',3,.)
local r=1

local denom=_N

foreach x of local samplevars {
	keep if `x'==1
	mat tab[`r',1]=`denom'-_N
	local denom=_N
	mat tab[`r',2]=_N
	duplicates report spid
	mat tab[`r',3]=r(unique_value)
	local r=`r'+1
}

mat rownames tab=`samplevars'

frmttable using "${outpath}\sample_derivation.rtf", statmat(tab) varlabels sdec(0) title("Sample Derivation") ///
ctitles("" "Interviews don't meet" "Interviews do meet" "Unique SPs") replace

save "${intpath}\dem_wave_1_5_w_help_sample.dta", replace

H="table 1"
//table 1

use "${intpath}\dem_wave_1_5_w_help_sample.dta", clear
label var tot_paidhrs "Hours per week of paid help, conditional on any"
egen adv_any=rowmax(adverse*)
egen adv_sc=rowmax(adverse_eat adverse_bath adverse_toil adverse_dres)
egen adv_hh=rowmax(adverse_laun adverse_shop adverse_meal adverse_bank)
egen adv_mob=rowmax(adverse_insd adverse_out adverse_bed)
label var adv_any "Any adverse consequence of unmet need"
label var adv_sc "Adverse consequence of unmet self-care need"
label var adv_hh "Adverse consequence of unmet household need"
label var adv_mob "Adverse consequence of unmet mobility need"
rename race_cat race
gen help_cat=ind_no_helpers
replace help_cat=2 if !ind_paid
replace help_cat=3 if n_helpers>n_paid
replace help_cat=4 if n_helpers==n_paid
label var help_cat "Caregiving, categorical"
label define help_cat 1 "No helpers" 2 "Informal help only" 3 "Informal & paid" ///
4 "Paid help only"
label values help_cat help_cat
label define income_quart 1 "Bottom quartile of income among NHATS sample, annual", modify
rename homebound_cat hb

local cvars1 age
local ivars1 female 
local catvars1 race /*education*/ livearrang
local cvars2
local ivars2 married educ_hs_ind medicaid medigap tricare
local catvars2
local cvars3 
local ivars3 
local catvars3 income_quart 
local cvars4 
local ivars4 prob_dem sr_phq2_depressed 
local catvars4 hb
local cvars5 n_helpers
local ivars5
local catvars5 help_cat
local cvars6 tot_hrswk_help_i tot_hrswk_paid_i  tot_paidhrs
local ivars6 ind_gt20hrs_wk_paid
local catvars6
local cvars7 adlcount_jenny iadlcount_jenny
local ivars7 adv_any adv_sc adv_hh adv_mob
local catvars7 wave

forvalues i=1/7 {
	foreach x of local catvars`i' {
		di "`x'"
		local `x'
		levelsof `x', local(levels)
		foreach l of local levels {
			gen `x'`l'=`x'==`l' if !missing(`x')
			local lab : label `x' `l'
			label var `x'`l' "`lab'"
			local `x' ``x'' `x'`l'
}
		di "``x''"
		local cativars`i' `cativars`i'' ``x''
}
}

di "`catvars1'"
di "`cativars1'"

forvalues i=1/7 {
local rows `rows' `cvars`i'' `ivars`i'' `cativars`i''
}

local rn : word count `rows' 1
local r=1
local c=1

mat tab=J(`rn',4,.)
mat stars=J(`rn',4,0)

local n=_N
forvalues i=1/7 {
	foreach x of local cvars`i' {
		sum `x', d
		mat tab[`r',`c']=r(mean)
		mat tab [`r',`c'+1]=r(sd)
		mat tab[`r',`c'+2]=r(p50)
		mat tab[`r',`c'+3]=`n'-r(N)
		local r=`r'+1
		
}
	foreach x of local ivars`i' {
		sum `x' 
		if r(N)*r(mean)>=11 mat tab[`r',`c']=r(mean)*100
		if r(N)*r(mean)>=11 mat tab[`r',`c'+3]=`n'-r(N)
		local r=`r'+1
}
	foreach x of local catvars`i' {
		sum `x'
		mat tab[`r',`c'+3]=`n'-r(N)
		foreach z of local `x' {
			sum `z' 
			if r(N)*r(mean)>=11 mat tab[`r',`c']=r(mean)*100
			local r=`r'+1
}
}
}
mat tab[`r',`c']=`n'
	

mat rownames tab=`rows' N

frmttable using "${outpath}\table_1`c(current_date)'.rtf", ///
statmat(tab) title("Characteristics of Decedents, by Dementia Status") ///
ctitles("" "Mean/%" "SD" "Median" "N Missing") varlabels ///
sdec(2,2,0,0) annotate(stars) asymbol(*,**) replace
